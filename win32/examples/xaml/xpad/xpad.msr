import("win32/base");
import("win32/lib");
import("win32/gdi");
import("win32/user");
import("win32/sysinfo");
import("promise");
import("rt/Windows.Foundation");
import("rt/Windows.Foundation.Collections");
import("rt/Windows.Graphics");
import("rt/Windows.Storage");
import("rt/Windows.Storage.Streams");
import("rt/Windows.Storage.Pickers");
import("rt/Windows.UI.Xaml");
import("rt/Windows.UI.Xaml.Hosting");
import("rt/Windows.UI.Xaml.Markup");
import("rt/Windows.UI.Xaml.Interop");
import("rt/Windows.UI.Xaml.Data");
import("rt/Windows.UI.Xaml.Input");
import("rt/Windows.UI.Xaml.Controls");
import("rt/Windows.UI.Xaml.Controls.Primitives");
import("rt/Microsoft.UI.Windowing");
import("rt/Microsoft.Web.WebView2.Core");
import("rt/moxaml");

import("windows");
import("winrt");
import("xaml");


class MyWindow : Window 
{
    MyWindow()
    {
        super.Window();

        this.self = io.dirname( io.self());

        this.dataContext = DataContext( 
        {
            "xamlSrc"   : nil,
            "xaml"      : io.slurp( this.self + "\\xpad_src.xaml" ),
            "ctx"       : io.slurp( this.self + "\\xpad.ctx" )
        });
    }

    // Xaml Event Handlers

    @Xaml(Element=UpdateButton,Event=Click)
    onUpdateButtonClick()
    {
        var xml = this.dataContext.xaml;
        var ctx = this.dataContext.ctx;
        var r = runtime.eval(ctx);

        var xaml = Xaml.load(xml,this);
        if(!xaml) return;

        var root = Windows.UI.Xaml.FrameworkElement( xaml );
        this.dataContext.xamlSrc = root;
        
        if(!r) return;

        root.DataContext = DataContext(r);

        if(r{"controller"} == nil) return;
        var c = r.controller;

        c.xamlWnd = this.xamlWnd;

        Xaml.bindEvents(root,c);
    }

    @Xaml(Element=SaveXaml,Event=Click)
    onSaveXaml()
    {
         // Xaml TextBox will convert newlines to single \r :-(
         var xml = os.toDos(this.dataContext.xaml);

         this.xamlWnd.FileSavePicker(
            { 
                "FileTypeChoices" : { "xaml" : [".xaml"] },
                "SuggestedFileName" : "xpad_src.xaml"
                //"FileName" : "C:\\Users\\mike\\source\\repos\\moc\\t\\xpad_src.xaml"
            })
        .then(
            fun(file) 
            {
                if( file.valid() )
                {
                    var p = file.Path;
                    io.flush(p,xml);
                }
            }
        );
    }

    @Xaml(Element=SaveCtx,Event=Click)
    onSaveCtx()
    {
         // Xaml TextBox will convert newlines to single \r :-(
         var xml = os.toDos(this.dataContext.ctx);

         this.xamlWnd.FileSavePicker(
            { 
                "FileTypeChoices" : { "xaml context" : [".ctx"] }
            })
        .then(
            fun(file) 
            {
                if( file.valid() )
                {
                    var p = file.Path;
                    io.flush(p,xml);
                }
            }
        );
    }

    @Xaml(Element=OpenXaml,Event=Click)
    onOpenXaml()
    {
         this.xamlWnd.FileOpenPicker(
            { 
                "FileTypeFilter" : [ ".xaml" ],
            })
        .then(
            fun(file) 
            {
                if( file.valid() )
                {
                    var p = file.Path;
                    var xml = io.slurp(p);
                    this.dataContext.xaml = xml;
                }
            }
        );
    }
    
    @Xaml(Element=OpenCtx,Event=Click)
    onOpenCtx()
    {
         this.xamlWnd.FileOpenPicker(
            { 
                "FileTypeFilter" : [ ".ctx" ],
            })
        .then(
            fun(file) 
            {
                if( file.valid() )
                {
                    var p = file.Path;
                    var ctx = io.slurp(p);
                    this.dataContext.ctx = ctx;
                }
            }
        );
    }

    // Win32 Messages

    @msg(WM_CREATE)
    onCreate(wParam,lParam)
    {
        // create Xaml child window on startup abd display xaml from file
        this.xamlWnd = XamlWindow( this, io.slurp( this.self + "\\xpad.xaml") );

        //support for custom titlebar - win11
        //Xaml.expand(this.hwnd,300);

    }

    @msg(WM_SIZE)
    onSize(wParam,lParam)
    {
        // resize Xaml child window if already created
        if(!this.xamlWnd || !this.xamlWnd.hwnd) return;
        print int(this.xamlWnd.hwnd);
        this.resizeChild( this.xamlWnd.hwnd);

        //support for custom titlebar - win11
        Xaml.expand(this.hwnd,300);

        return 0;
    }
    
    @msg(WM_DESTROY)
    onDestroy(wParam,lParam)
    {
        Win32.PostQuitMessage(0);
        return 0;
    }
}


var wnd = MyWindow();

wnd.create( "Xaml Pad");
wnd.show();
run(wnd);


print "OK";
